buildscript {
    repositories {
        mavenCentral()
        maven {
            name "gradle plugins"
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name "central snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.0'
    }
}
plugins {
    id "net.ltgt.apt" version "0.9"
    id "com.github.hierynomus.license" version "0.13.1"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'aversion-util'

util {
    javaVersion = '1.8'
}
idea.project.languageLevel = util.javaVersion

repositories {
    mavenCentral()
    maven {
        name "central snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name "local files"
        url "libs"
    }
}

dependencies {
    // Dependencies from /libs. These are copied from the FRC installation folder in your home directory.
    compile group: 'edu.wpi', name: 'cscore', version: 'NONE'
    compile group: 'edu.wpi', name: 'CTRLib', version: 'NONE'
    compile group: 'edu.wpi', name: 'NetworkTables', version: 'NONE'
    compile group: 'edu.wpi', name: 'opencv', version: 'NONE'
    compile group: 'edu.wpi', name: 'WPILib', version: 'NONE'
    compile group: 'com.kuailabs', name: 'navx_frc', version: 'NONE'
    
    // Neat graph util
    compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'

    // Auto-value!
	compileOnly group: 'com.google.auto.value', name: 'auto-value', version: '1.4-rc2'
    apt group: 'com.google.auto.value', name: 'auto-value', version: '1.4-rc2'

    testCompile group: 'junit', name: 'junit', version: '4.+'
}

// Add eclipse FRC nature
eclipse.project.file.beforeMerged { proj ->
    proj.natures.add('edu.wpi.first.wpilib.plugins.core.nature.FRCProjectNature')
}

// Move source into src
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

// Setup compiling to be better.
configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}

// License configuration

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// configurations for the ant-replacement code
ext.packageNumber = '5818'
ext.teamNumber = project.findProperty('teamNumber') ?: packageNumber
apply from: 'ant-replacement.gradle', to: project
